# This program is part of the
 
     Revised Mathematisch Centrum Algol 68 Test Set

  by Dick Grune [1979].

  This program is distributed with Algol 68 Genie with
  kind permission of Dick Grune.

  This test set is available from:

     http://www.dickgrune.com/CS/Algol68

  or from the Vrije Universiteit Amsterdam:

     ftp://ftp.cs.vu.nl/pub/dick/Algol68/ #

PR quiet PR

#appl09#
BEGIN # Bubble sort, ALGOL 68 version #
      PROC sort = (REF [] INT a) VOID:
           BEGIN INT p = LWB a;
                 
                 FOR dp FROM p + 1 TO UPB a 
                 DO INT bp := dp;
                    
                    INT bubble = a[bp];
                    
                    WHILE INT prev;
                          
                          IF bp = p
                          THEN FALSE
                          ELSE prev := a[bp - 1];
                               prev > bubble
                          FI
                    DO a[bp] := prev;
                       bp -:= 1
                    OD;
                    a[bp] := bubble
                 OD
           END # sort # ;
      
      PROC shuffle = (REF [] INT a) VOID:
           BEGIN INT p = LWB a, q = UPB a;
                 
                 FOR i FROM q BY -1 TO p + 1 
                 DO REF INT t = a[ENTIER (random * (i - p + 1)) + p], u = a[i];
                    
                    INT h = t;
                    
                    t := u;
                    u := h
                 # swap #
                 OD
           END # shuffle # ;
      
      INT max = 8;
      
      [1 : max] INT p;
      
      PROC test = (PROC (INT) INT a) VOID:
           (FOR i TO max 
            DO p[i] := a (i)
            OD;
            shuffle (p);
            print (("Shuffled:", newline, p, newline));
            sort (p);
            print (("Sorted:", newline, p, newline, newline)));
      
      test ((INT p) INT: p);
      test ((INT p) INT: ENTIER (p / 5));
      test ((INT p) INT: 0)
END