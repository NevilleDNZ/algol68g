# This program is part of the Revised Mathematisch Centrum Algol 68 Test Set
  by Dick Grune [1979].

  This program is distributed with Algol 68 Genie with
  kind permission of Dick Grune.'

  These test sets are available from:
     www.dickgrune.com/CS/Algol68
  or from the Vrije Universiteit Amsterdam:
     ftp://ftp.cs.vu.nl/pub/dick/Algol68/ #

PR quiet PR

#stow07#
BEGIN # Test  +  and  =  on strings #
      PROC equal = (STRING a, b) BOOL:
           (INT p = UPB a - LWB a + 1, q = UPB b - LWB b + 1;
            
            INT r = (p > 0 | p | 0), s = (q > 0 | q | 0);
            
            IF r /= s
            THEN FALSE
            ELSE BOOL c := TRUE;
                 
                 INT la = LWB a - 1, lb = LWB b - 1;
                 
                 FOR i TO r 
                 WHILE c := a[i + la] = b[i + lb]
                 DO SKIP
                 OD;
                 c
            FI);
      
      PROC concat = (STRING a, b) STRING:
           (INT p = UPB a - LWB a + 1, q = UPB b - LWB b + 1;
            
            INT r = (p > 0 | p | 0), s = (q > 0 | q | 0);
            
            [r + s] CHAR c;
            
            (c[1 : p@LWB a] := a, c[r + 1 : r + q@LWB b] := b);
            c);
      
      print (("This program should print a 25 * 25 block of stars.", newline, newline));
      FOR lwb a FROM -2 TO 2 
      DO FOR sze a FROM -2 TO 2 
         DO print (newline);
            FOR lwb b FROM -2 TO 2 
            DO FOR sze b FROM -2 TO 2 
               DO STRING a = "1a"[1 : sze a@lwb a], b = "1b"[1 : sze b@lwb b];
                  
                  print ("*");
                  # to estimate progress #
                  
                  # test = #
                  IF (a = b) = equal (a, b)
                  THEN SKIP
                  ELSE print ((newline, "Error in string comparison: ", "should be ", (equal (a, b) | "" | "un"), "equal", ", are ", (a = b | "" | "un"), "equal"));
                       GOTO bad
                  FI;
                  # test + #
                  IF a + b = concat (a, b)
                  THEN SKIP
                  ELSE print ((newline, "Error in string concatenation: ", "should be """, concat (a, b), """, is """, a + b, """"));
                       GOTO bad
                  FI EXIT
                  bad:
                  print ((newline, "  first string is """, a, """, lwb=", whole (LWB a, 4), ", upb =", whole (UPB a, 4), ", second string is """, b, """, lwb=", whole (LWB b, 4), ", upb =", whole (UPB b, 4), newline))
               OD
            OD
         OD
      OD
END