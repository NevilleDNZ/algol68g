# This program is part of the Revised Mathematisch Centrum Algol 68 Test Set
  by Dick Grune [1979].

  This program is distributed with Algol 68 Genie with
  kind permission of Dick Grune.'

  These test sets are available from:
     www.dickgrune.com/CS/Algol68
  or from the Vrije Universiteit Amsterdam:
     ftp://ftp.cs.vu.nl/pub/dick/Algol68/ #

PR quiet PR

#appl01#
BEGIN # ALGOL 68 program, TJD 730705.
          Calculates all increasing sequences
          adding up to a given integer from 1 to 10 #
      [1 : 4] INT a;
      
      PROC print solution = (INT p) VOID:
           print ((a[1 : p], newline));
      
      PROC build up = (INT p, rest) VOID:
           IF rest = 0
           THEN print solution (p)
           ELSE FOR k FROM (p = 0 | 1 | a[p] + 1) TO rest 
                DO (a[p + 1] := k;
                    build up (p + 1, rest - k))
                OD
           FI;
      
      FOR g TO 10 
      DO print ((newline, g, " =", newline));
         build up (0, g)
      OD
# For an ALGOL 60 program yielding the same output
    see Th.J. Dekker, Syllabus Informatica,
    Instituut voor Toepassingen van de Wiskunde,
    Universiteit van Amsterdam, 1972, page 81 - 82 #
END