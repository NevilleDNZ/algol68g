# This program is part of the Revised Mathematisch Centrum Algol 68 Test Set
  by Dick Grune [1979].

  This program is distributed with Algol 68 Genie with
  kind permission of Dick Grune.'

  These test sets are available from:
     www.dickgrune.com/CS/Algol68
  or from the Vrije Universiteit Amsterdam:
     ftp://ftp.cs.vu.nl/pub/dick/Algol68/ #

  PR quiet PR

#smio06#
BEGIN
# 10/08/73, R van Vliet; 30/09/75, revised.
Test the print and putroutines.#
    INT n dots = 10;
    print(("Print ", whole(n dots, -2), " dots on the next line",
              newline));
    ( PROC spacedot =(REF FILE f) VOID:
      #This procedure is used to print 'n dots' dots in a
      highly recursive call on 'print'.
      First the current position is moved to 'n dots' by printing
      spaces, second the dots are printed from right to left.#
     IF char number(f) < n dots
     THEN space(f); put(f, spacedot)
     ELSE #The spaces are done, now we turn to dotter.#
            put(f, dotter)
     FI,
    PROC dotter =(REF FILE f) VOID:
     IF char number(f) > 1
     THEN put(f, "."); backspace(f);
       put(f, (backspace, dotter))
     ELSE put (f, ".")
      FI;
      print((spacedot, newline))
    )
END
