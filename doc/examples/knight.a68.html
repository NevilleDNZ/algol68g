<html>
<body bgcolor="black">
<font color="lime">
<pre>
# Show paths by which a knight can traverse a n*n board without passing a square twice. #

PR timelimit=60 PR

INT n = 5, empty = 0; 
[n, n] INT board;

FOR i TO n
DO FOR j TO n
   DO board[i, j] := empty
   OD
OD;

INT solutions := 0;

PROC go to square = (INT u, v, num) VOID:
  # Backtracking routine to traverse board. #
  IF (u >= 1 AND u <= n ANDF v >= 1 AND v <= n) ANDF board[u, v] = empty
  THEN board[u, v] := num;  
       IF num = n * n
       THEN printf (($l"solution"xg(0)n(n)(ln(n)(x2d))$, solutions +:= 1, board))
       ELSE go to square (u + 1, v + 2, num + 1);
            go to square (u + 2, v + 1, num + 1);
            go to square (u + 2, v - 1, num + 1);
            go to square (u + 1, v - 2, num + 1);
            go to square (u - 1, v - 2, num + 1);
            go to square (u - 2, v - 1, num + 1);
            go to square (u - 1, v + 2, num + 1);
            go to square (u - 2, v + 1, num + 1)
       FI;
       board[u, v] := empty
  FI;

go to square (1, 1, 1)
</pre>
</font>
</body>
</html>
