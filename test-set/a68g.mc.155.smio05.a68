# This program is part of the Revised Mathematisch Centrum Algol 68 Test Set
  by Dick Grune [1979].

  This program is distributed with Algol 68 Genie with
  kind permission of Dick Grune.'

  These test sets are available from:
     www.dickgrune.com/CS/Algol68
  or from the Vrije Universiteit Amsterdam:
     ftp://ftp.cs.vu.nl/pub/dick/Algol68/ #

  PR quiet PR

#smio05#
BEGIN
# 10/08/73, R van Vliet; 30/09/75, revised.
Test the print and putroutines.#
 # Assumes pages more than twice as wide as they are high.#
    INT max ch n = # actual max char, formerly
        max char[standout channel] #
    (FILE f:= standout; INT i;
        on line end(f, (REF FILE f)BOOL: GOTO out);
        DO put(f, space) OD;
    out: i:= char number(f) -1; TO i DO put(f, backspace) OD; i);
  print(("Some tests on PROC(REF FILE)VOID''s", newline));
    ( PROC triangle =(REF FILE f)VOID:
      BEGIN
        FILE rf:=( line number(f) = 1 AND char number(f) = 1 | f
                   | FILE ff:=f; on page end(ff,
                              (REF FILE f)BOOL : GOTO out);
                   ff);
        PROC nlp =(REF FILE f) VOID:
          new line(f);
        nlp(rf);
        INT half width = max ch n OVER 2;
        INT i:=1;
        FOR k FROM half width -1 BY -1 TO 0
        DO
          TO k DO space(rf) OD;
          TO i DO put(rf, ".") OD;
          i +:= 2;
          new line(rf)
        OD EXIT
      out: new line(f)
      END;
      print(("First print the full triangle", new page));
      print(triangle);
      print((new line,
        "Now a part of it, to check some administration.", new line,
        "The triangle should be chopped at the end of the page.",
          newline));
      print(triangle);
      print((
         "Now print the triangle as part of a more complicated call.",
         new line, triangle,
         "Did it stop at end of page again ?", newline))
    )
END
