# This program is part of the Revised Mathematisch Centrum Algol 68 Test Set
  by Dick Grune [1979].

  This program is distributed with Algol 68 Genie with
  kind permission of Dick Grune.'

  These test sets are available from:
     www.dickgrune.com/CS/Algol68
  or from the Vrije Universiteit Amsterdam:
     ftp://ftp.cs.vu.nl/pub/dick/Algol68/ #

PR quiet PR

#appl12#
BEGIN # AvW, 1974:10:23, packing small integers into a larger integer#
      PROC code = ([] INT sequence) INT:
           (INT code := 0;
            
            FOR k TO UPB sequence 
            DO code *:= 2 +:= 1 *:= 2 ** sequence[k]
            OD;
            code),
      PROC length = (INT code) INT:
           (INT length := 0, c := code;
            
            WHILE c > 0
            DO (ODD c | length +:= 1);
               c OVERAB 2
            OD;
            length),
      PROC sequence = (INT code) [] INT:
           (INT l := length (code), c := code;
            
            [1 : l] INT sequence;
            
            FOR k TO l 
            DO sequence[k] := 0
            OD;
            WHILE c > 0
            DO (ODD c | l -:= 1 | sequence[l] +:= 1);
               c OVERAB 2
            OD;
            sequence);
      
      FOR c FROM 0 TO 100 
      DO print ((c, length (c), sequence (c), newline, code (sequence (c)), newline, newline))
      OD
END