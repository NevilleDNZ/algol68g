# This program is part of the
 
     Revised Mathematisch Centrum Algol 68 Test Set

  by Dick Grune [1979].

  This program is distributed with Algol 68 Genie with
  kind permission of Dick Grune.

  This test set is available from:

     http://www.dickgrune.com/CS/Algol68

  or from the Vrije Universiteit Amsterdam:

     ftp://ftp.cs.vu.nl/pub/dick/Algol68/ #

PR quiet PR

#simp16#

PR norun PR

#   321   314159.265e-5   t    1.1  i 2.2
ongeluksgetal
aap:noot   654
1   2   3   4   5   6
10   20   30   40
  The above is input for the following program #
(# Simple unformatted transput #
 MODE TERMSTRING = STRUCT (STRING string, CHAR term);
 
 CHAR ch,
 INT i,
 REAL r,
 BOOL b,
 COMPL z,
 [1 : 13] CHAR rowch,
 STRUCT (TERMSTRING s, t, INT i) struct,
 [1 : 2] STRUCT (INT i, STRUCT (INT i, j) j) rowstruct;
 
 [1 : 2] INT a1, a2;
 
 make term (stand in, " :");
 read (ch);
 read (i);
 read (r);
 read (b);
 read (z);
 read (newline);
 print ((ch, i, r, "     ", b, "    ", z, newline));
 read ((rowch, newline));
 print ((rowch, newline));
 read ((struct, newline));
 print ((struct, newline));
 read ((rowstruct, newline));
 print ((rowstruct, newline));
 FOR n FROM 4 BY -4 TO -4 
 DO # 4, 0, -4 # print ((newline, newline, whole (i, n), "     ", fixed (r, n, 2), "     ", float (r, n, 2, 2)))
 OD;
 FOR n TO 4 
 DO read (IF ODD n
          THEN a1[n OVER 2 + 1]
          ELSE a2[n OVER 2]
          FI)
 OD;
 print ((newpage, a1, newline, a2, newline, "End")))