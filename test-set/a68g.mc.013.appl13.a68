# This program is part of the
 
     Revised Mathematisch Centrum Algol 68 Test Set

  by Dick Grune [1979].

  This program is distributed with Algol 68 Genie with
  kind permission of Dick Grune.

  This test set is available from:

     http://www.dickgrune.com/CS/Algol68

  or from the Vrije Universiteit Amsterdam:

     ftp://ftp.cs.vu.nl/pub/dick/Algol68/ #

PR quiet PR

#appl13#

#   Ring code   #
BEGIN INT m := 4;
      
      m := 2 ** m;
      INT n  # left-most bit # = m OVER 2;
      
      [0 : m - 1] BOOL f;
      
      [1 : m] INT t;
      
      FOR i FROM 0 TO m - 1 
      DO f[i] := TRUE
      OD;
      PROC p = (INT i, k) VOID:
           BEGIN t[k] := i;
                 f[i] := FALSE;
                 IF k = m
                 THEN print (newline);
                      # Every bit column in 't' now contains the ring code #
                      FOR k TO m 
                      DO print (t[k] GE n)
                      OD
                 ELSE INT l;
                      
                      IF f[l := 2 * i MOD m]
                      THEN p (l, k + 1)
                      FI;
                      IF f[l +:= 1]
                      THEN p (l, k + 1)
                      FI
                 FI;
                 f[i] := TRUE
           END;
      
      p (0, 1)
END