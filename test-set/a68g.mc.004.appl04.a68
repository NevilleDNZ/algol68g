# This program is part of the Revised Mathematisch Centrum Algol 68 Test Set
  by Dick Grune [1979].

  This program is distributed with Algol 68 Genie with
  kind permission of Dick Grune.'

  These test sets are available from:
     www.dickgrune.com/CS/Algol68
  or from the Vrije Universiteit Amsterdam:
     ftp://ftp.cs.vu.nl/pub/dick/Algol68/ #

PR quiet PR

#appl04#
BEGIN # 1. Sets in ALGOL 68; 2. Pebble problem of E.W. Dijkstra #
      MODE RED = REF STRUCT (RED red),
            WHITE = REF STRUCT (WHITE white),
            BLUE = REF STRUCT (BLUE blue);
      
      MODE STONE = UNION (RED, WHITE, BLUE);
      
      PROC sort = (REF [] STONE st) VOID:
           (INT pr := 1, pw := 1, pb := UPB st;
            
            PRIO EXCH = 1;
            
            OP EXCH = (REF STONE a, b) VOID:
               (STONE c = b; b := a; a := c);
            
            TO UPB st 
            DO CASE st[pw]
               IN (RED): (st[pr] EXCH st[pw];
                          pr +:= 1;
                          pw +:= 1), (WHITE): pw +:= 1, (BLUE): (st[pw] EXCH st[pb];
                                                                 pb -:= 1)
               ESAC
            OD);
      
      OP PRINT = (REF [] STONE st) VOID:
         (print (newline);
          FOR i TO UPB st 
          DO print ((st[i] | (RED): "r", (WHITE): "w", (BLUE): "b"))
          OD);
      
      INT n = 20;
      
      [1 : n] STONE stone;
      
      FOR i TO UPB stone 
      DO stone[i] := (ENTIER (random * 3) + 1 | RED (NIL), WHITE (NIL), BLUE (NIL))
      OD;
      PRINT stone;
      sort (stone);
      PRINT stone
END