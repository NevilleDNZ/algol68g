COMMENT

This program is part of the Algol 68 Genie test set.

A small selection of the Algol 68 Genie regression test set is distributed 
with Algol 68 Genie. The purpose of those programs is to perform some checks 
to judge whether A68G behaves as expected.
None of these programs should end ungraciously with for instance an 
addressing fault.

COMMENT

PR quiet regression PR

PROC partition = (REF [] DATA array, PROC (REF DATA, REF DATA) BOOL cmp) INT: 
 (INT begin := LWB array, end := UPB array;
  WHILE begin < end
  DO BOOL go on := TRUE;
     WHILE begin < end AND go on 
     DO IF cmp (array[begin], array[end]) 
        THEN DATA tmp = array[begin];
             array[begin] := array[end];
             array[end] := tmp;
             go on := FALSE
        FI;
        end -:= 1
     OD;
     go on := TRUE;
     WHILE begin < end AND go on
     DO IF cmp (array[begin], array[end]) 
        THEN DATA tmp = array[begin];
             array[begin] := array[end];
             array[end] := tmp;
             go on := FALSE
        FI;
        begin +:= 1
     OD
  OD;
  begin
 );
 
PROC qsort = (REF [] DATA array, PROC (REF DATA, REF DATA) BOOL cmp) VOID: 
  IF LWB array < UPB array
  THEN INT i := partition (array, cmp);
       PAR (qsort (array[ : i - 1], cmp), qsort (array[i + 1 : ], cmp))
  FI;
 
MODE DATA = INT;
PROC cmp = (REF DATA a, b) BOOL: a > b;
 
[] DATA const l = (8, 7, 5, 6, 9, 4, 2, 3, 1);
[UPB const l] DATA l := const l;
qsort (l, cmp);
printf (($-zd$, l))

