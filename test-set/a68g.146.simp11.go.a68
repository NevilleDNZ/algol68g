# This program is part of the Revised Mathematisch Centrum Algol 68 Test Set
  by Dick Grune [1979].
 
  This program is distributed with Algol 68 Genie with
  kind permission of Dick Grune.
 
  The complete test set is an open-access publication, 
  self-archived by Dick Grune and available from:
     www.dickgrune.com/CS/Algol68
  or from the Vrije Universiteit Amsterdam:
     ftp://ftp.cs.vu.nl/pub/dick/Algol68/ #
 
#simp11#
BEGIN # Translation decimal number to Roman notation and vice versa #
      PROC roman = (INT number) STRING:
           BEGIN INT n := number,
                 STRING result,
                 [] STRUCT (INT value, STRING r) table = ((1000, "M"),
                                                          (900, "CM"),
                                                          (500, "D"),
                                                          (400, "CD"),
                                                          (100, "C"),
                                                          (90, "XC"),
                                                          (50, "L"),
                                                          (40, "XL"),
                                                          (10, "X"),
                                                          (9, "IX"),
                                                          (5, "V"),
                                                          (4, "IV"),
                                                          (1, "I"));
                 
                 FOR i TO UPB table 
                 DO INT v = value OF table[i],
                    STRING r = r OF table[i];
                    
                    WHILE v LE n
                    DO (result +:= r, n -:= v)
                    OD
                 OD;
                 result
           END,
      PROC value of roman = (STRING text) INT:
           IF text = ""
           THEN 0
           ELSE OP ABS = (CHAR s) INT:
                   CASE INT p;
                        
                        char in string (s, p, "IVXLCDM");
                        p
                   IN 1, 5, 10, 50, 100, 500, 1000
                   ESAC,
                PROC char in string = (CHAR c, REF INT i, STRING s) BOOL:
                     (FOR k TO UPB s 
                      DO (c = s[k] | i := k; l)
                      OD;
                      FALSE EXIT
                      l: TRUE);
                
                INT v, maxv := 0, maxp;
                
                FOR p TO UPB text 
                DO IF (v := ABS text[p]) > maxv
                   THEN maxp := p;
                        maxv := v
                   FI
                OD;
                maxv - value of roman (text[ : maxp - 1]) + value of roman (text[maxp + 1 : ])
           FI;
      
      print (roman (1968));
      # "MCMLXVIII" # print (value of roman ("MCMLXXIII"))
#1973#
END