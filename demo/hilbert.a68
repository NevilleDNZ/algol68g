COMMENT
An application for multi-precision LONG LONG INT. 
Calculate exact determinant of Hilbert matrices using fractions.
COMMENT

BEGIN

# 
Fraction data structure and denotation through the "DIV" operator.
A fraction has positive denominator; the nominator holds the sign.
#

      MODE FRAC = STRUCT (NUM nom, den), NUM = LONG LONG INT;

      OP N = (FRAC u) NUM: nom OF u,
         D = (FRAC u) NUM: den OF u;

      PR precision=101 PR # NUM can hold a googol! #
 
      FRAC zero = 0 DIV 1, one = 1 DIV 1;

      OP DIV = (NUM n, d) FRAC: 
         IF d = 0
         THEN print ("Zero denominator"); stop 
         ELSE NUM gcd = ABS n GCD ABS d;
              (SIGN n * SIGN d * ABS n OVER gcd, ABS d OVER gcd)
         FI;

      OP DIV = (INT n, d) FRAC: NUM (n) DIV NUM (d);

      PRIO DIV = 2;

      OP GCD = (NUM a, b) NUM:
         IF b = 0 
         THEN ABS a
         ELSE b GCD (a MOD b)
         FI;

      PRIO GCD = 8;
  
# Basic operators for fractions. #
 
      OP - = (FRAC u) FRAC: - N u DIV D u;
  
      OP + = (FRAC u, v) FRAC: N u * D v + N v * D u DIV D u * D v;
  
      OP - = (FRAC u, v) FRAC: u + - v;

      OP * = (FRAC u, v) FRAC: N u * N v DIV D u * D v;

      OP / = (FRAC u, v) FRAC: u * (D v DIV N v);

      OP +:= = (REF FRAC u, FRAC v) REF FRAC: u := u + v;

      OP -:= = (REF FRAC u, FRAC v) REF FRAC: u := u - v;
  
      OP *:= = (REF FRAC u, FRAC v) REF FRAC: u := u * v;

      OP /:= = (REF FRAC u, FRAC v) REF FRAC: u := u / v;

      OP = = (FRAC u, v) BOOL: N u = N v ANDF D u = D v;
  
      OP /= = (FRAC u, v) BOOL: NOT (u = v);
  
# Matrix algebra. #

      OP INNER = ([] FRAC u, v) FRAC:
         # Innerproduct of two arrays of rationals #
         BEGIN FRAC s := zero;
               FOR i TO UPB u
               DO s +:= u[i] * v[i]
               OD;
               s
         END;

      PRIO INNER = 8;
  
      PROC lu decomposition = (REF [, ] FRAC a, REF [] INT p) VOID:
           # LU-decomposition cf. Crout, of a matrix of rationals. #
           BEGIN INT n = 1 UPB a;
                 FOR k TO n
                 DO FRAC piv := zero, INT k1 := k - 1;
                    REF INT pk = p[k];
                    REF [] FRAC aik = a[, k], aki = a[k,];
                    FOR i FROM k TO n
                    DO aik[i] -:= a[i, 1 : k1] INNER aik[1 : k1];
                       IF piv = zero ANDF aik[i] /= zero
                       THEN piv := aik[i]; 
                            pk := i
                       FI
                    OD;
                    IF piv = zero
                    THEN print((newline, "Singular matrix")); stop
                    FI;
                    IF pk /= k
                    THEN FOR i TO n
                         DO FRAC r = aki[i];
                            aki[i] := a[pk, i]; 
                            a[pk, i] := -r
                         OD
                    FI;
                    FOR i FROM k + 1 TO n
                    DO aki[i] -:= aki[1 : k1] INNER a[1 : k1, i] /:= piv
                    OD 
                 OD
           END;
  
      PROC determinant = ([,] FRAC a) FRAC:
           # Determinant of a decomposed matrix is its trace. #
           BEGIN FRAC d := one;
                 FOR i TO 1 UPB a
                 DO d *:= a[i, i]
                 OD;
                 d
           END;

# Table of required results. #  
  
      [] NUM table = BEGIN
         1,
         12,
         2 160,
         6 048 000,
         266 716 800 000,
         186 313 420 339 200 000, 
         2 067 909 047 925 770 649 600 000,
         365 356 847 125 734 485 878 112 256 000 000, 
         1 028 781 784 378 569 697 887 052 962 909 388 800 000 000, 
         46 206 893 947 914 691 316 295 628 839 036 278 726 983 680 000 000 000
      END;

# Compute determinant of Hilbert matrix of increasing rank. #
   
      FOR n TO UPB table
      DO [1 : n, 1 : n] FRAC a;
         FOR i TO n
         DO a[i, i] := 1 DIV 2 * i - 1;
            FOR j FROM i + 1 TO n
            DO a[i, j] := a[j, i] := 1 DIV i + j - 1
            OD
         OD;
         lu decomposition(a, LOC [1 : n] INT);
         FRAC det = determinant (a);
         print(("Rank ", whole (n, 0), 
                ", determinant ", whole (N det, 0), " / ", whole (D det, 0),
                (N det = 1 AND D det = table[n] | ", ok" | ", not ok" ), newline))
      OD
END
