# This program is part of the Revised Mathematisch Centrum Algol 68 Test Set
  by Dick Grune [1979].

  This program is distributed with Algol 68 Genie with
  kind permission of Dick Grune.'

  These test sets are available from:
     www.dickgrune.com/CS/Algol68
  or from the Vrije Universiteit Amsterdam:
     ftp://ftp.cs.vu.nl/pub/dick/Algol68/ #

PR quiet PR

#appl16#
BEGIN # Sheep in mountain cleft #
      
      # See A. van Wijngaarden, Programmacorrectheid en grammatica's,
          in Mathematical Centre Syllabus 21, XII, 1975. #
      PROC p = (INT i, j, STRING s) VOID:
           # i is line number, j is the position of the dot in s;
                 three spaces have been appended to the left and the right
                 of s, in order to simplify the testing #
           BEGIN print ((newline, i, " ", s[4 : n]));
                 IF s[j - 2 : j] = "><."
                 # h6 #
                 THEN p (i + 1, j - 2, s[ : j - 3] + ".<>" + s[j + 1 : ])
                 ELIF s[j : j + 2] = ".><"
                 # h7 #
                 THEN p (i + 1, j + 2, s[ : j - 1] + "<>." + s[j + 3 : ])
                 ELIF s[j - 1 : j + 3] = ">.<><"
                 # h4 #
                 THEN p (i + 1, j - 1, s[ : j - 2] + ".>" + s[j + 1 : ])
                 ELIF s[j - 3 : j + 1] = "><>.<"
                 # h5 #
                 THEN p (i + 1, j + 1, s[ : j - 1] + "<." + s[j + 2 : ])
                 ELIF s[j - 1 : j + 1] = ">.<"
                 # h4, h5 #
                 THEN print (newline);
                      p (i + 1, j - 1, s[ : j - 2] + ".>" + s[j + 1 : ]);
                      print (newline);
                      p (i + 1, j + 1, s[ : j - 1] + "<." + s[j + 2 : ])
                 ELIF s[j - 1 : j] = ">."
                 # h8 #
                 THEN p (i + 1, j - 1, s[ : j - 2] + ".>" + s[j + 1 : ])
                 ELIF s[j : j + 1] = ".<"
                 THEN p (i + 1, j + 1, s[ : j - 1] + "<." + s[j + 2 : ])
                 # h9 #
                 FI
           END # p # ;
      
      INT a, b;
      
      # read((a, b)); # a := 3;
      b := 3;
      INT n = a + b + 7;
      
      IF a >= 0 AND b >= 0
      THEN p (1, a + 4, "   " + a * ">" + "." + b * "<" + "   ")
      FI
END