# This program is part of the Revised Mathematisch Centrum Algol 68 Test Set
  by Dick Grune [1979].
 
  This program is distributed with Algol 68 Genie with
  kind permission of Dick Grune.
 
  The complete test set is an open-access publication, 
  self-archived by Dick Grune and available from:
     www.dickgrune.com/CS/Algol68
  or from the Vrije Universiteit Amsterdam:
     ftp://ftp.cs.vu.nl/pub/dick/Algol68/ #
 
#appl03#
BEGIN # ALGOL 68 program TJD 730701.
          This program prints a difference table
          of a 4-th degree polynomial. #
      [0 : 5] INT a;
      
      OP MIM = (INT a, b) INT:
         (a <= b | a | b);
      
      PRIO MIM = 1;
      
      PROC pol4 = (INT x) INT:
           x * (x + 1) * (x + 2) * (x + 3);
      
      FOR n FROM 0 TO 20 
      DO INT kmax = n MIM 5;
         
         [0 : kmax] INT b;
         
         b[0] := pol4 (n);
         FOR k TO kmax 
         DO b[k] := b[k - 1] - a[k - 1]
         OD;
         a[0 : kmax AT 0] := b;
         print ((n, b, newline))
      OD
END